# Maintainer: jdarch <jda -dot- cloud -plus- archlinux -at- gmail -dot- com>

pkgname=('libudev0')
pkgver=182
pkgrel=2
pkgdesc="Dynamic library to access udev device information, legacy libudev.so.0."
url="https://www.kernel.org/pub/linux/utils/kernel/hotplug/udev/udev.html"
arch=('x86_64' 'i686')
license=('GPL')
depends=('glibc')
makedepends=('gperf' 'kmod' 'util-linux' 'usbutils')
source=("https://www.kernel.org/pub/linux/utils/kernel/hotplug/udev-182.tar.xz")
md5sums=('023877e6cc0d907994b8c648beab542b')
sha512sums=('2420ff16a596ca4dc2ad6c9e0142fcd62e49102cbe1a1cbef8599ea04f3cfe0f26df7f548a9734fd6e5dc5aa126846793acea7dfcf3021604f4e40a47b1f8aa9')

prepare() {
   cd "${srcdir}/udev-${pkgver}"
   autoreconf -vfi
}

build() {
  cd "${srcdir}/udev-${pkgver}"
  ./configure \
    --prefix='/usr' \
    --libdir='/usr/lib' \
    --libexecdir='/usr/lib/udev' \
    --sysconfdir='/etc' \
    --disable-gudev \
    --disable-introspection \
    --with-systemdsystemunitdir='/usr/lib/systemd/system'
  make LIBS="-lrt"
}

package() {
  cd "${srcdir}/udev-${pkgver}"
  install -Dm755 .libs/libudev.so.0.13.1 "${pkgdir}/usr/lib/libudev.so.0.13.1"
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
  ln -s "/usr/lib/libudev.so.0.13.1" "${pkgdir}/usr/lib/libudev.so.0"
}