# Maintainer: Adam Reichold <adam.reichold@t-online.de>

pkgbase=qpdfview
pkgname=('qpdfview' 'qpdfview-djvu-plugin' 'qpdfview-ps-plugin')
pkgver=0.4.14
pkgrel=3
pkgdesc='A tabbed PDF viewer using the poppler library.'
arch=('i686' 'x86_64')
url='https://launchpad.net/qpdfview'
license=('GPL2')
depends=('libcups' 'poppler-qt5' 'qt5-svg' 'desktop-file-utils' 'hicolor-icon-theme' 'libxkbcommon-x11')
optdepends=('qpdfview-ps-plugin: qpdfview plugin for PostScript support' 
            'qpdfview-djvu-plugin: qpdfview plugin for DjVu support')
makedepends=('qt5-tools' 'libspectre' 'djvulibre')
install='qpdfview.install'
source=("http://launchpad.net/$pkgname/trunk/$pkgver/+download/$pkgname-$pkgver.tar.gz")
md5sums=('f87802101e2a888f86b45ee28b802d2f')

build() {
    cd "$srcdir/$pkgname-$pkgver"
    lrelease-qt5 qpdfview.pro
    qmake-qt5 "CONFIG+=$config" qpdfview.pro
    make
}

package_qpdfview() {
    cd "$srcdir/$pkgbase-$pkgver"
    make "INSTALL_ROOT=$pkgdir" install
    rm -f "${pkgdir}/usr/lib/qpdfview/libqpdfview_djvu.so"
    rm -f "${pkgdir}/usr/lib/qpdfview/libqpdfview_ps.so"
}

package_qpdfview-djvu-plugin() {
    depends=('qpdfview' 'djvulibre')
    cd "$srcdir/$pkgbase-$pkgver"
    install -Dm644 "libqpdfview_djvu.so" "${pkgdir}/usr/lib/qpdfview/libqpdfview_djvu.so"
}

package_qpdfview-ps-plugin() {
    depends=('qpdfview' 'libspectre')
    cd "$srcdir/$pkgbase-$pkgver"
    install -Dm644 "libqpdfview_ps.so" "${pkgdir}/usr/lib/qpdfview/libqpdfview_ps.so"
}