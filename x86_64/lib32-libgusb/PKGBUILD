# Maintainer: jtts
# Contributor: josephgbr <rafael.f.f1@gmail.com>
# Contributor: Ionut Biru <ibiru@archlinux.org>

_pkgbasename=libgusb
pkgname=lib32-$_pkgbasename
pkgver=0.2.4
pkgrel=2
pkgdesc="GLib wrapper around libusb1 (32-bit)"
arch=(x86_64)
url="https://github.com/hughsie/libgusb"
license=(LGPL2.1)
depends=("lib32-systemd" "lib32-libusb" "$_pkgbasename>=0.2.4")
makedepends=('gcc-multilib' 'gobject-introspection' 'python2')
source=(http://people.freedesktop.org/~hughsient/releases/$_pkgbasename-$pkgver.tar.xz)
sha256sums=('437966fb45e30b8e757b2c200daf5d4e7dd22bf5aa80e151e3b1807270d38083')

build() {
  export CC='gcc -m32'
  cd $_pkgbasename-$pkgver
  ./configure --prefix=/usr --disable-static --libdir=/usr/lib32
  make
}

package() {
  make -C $_pkgbasename-$pkgver DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/usr/{include,share}
}