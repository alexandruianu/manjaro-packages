# Maintainer:  Martin Wimpress <code@flexion.org>
# Contributor: josephgbr <rafael.f.f1@gmail.com>

_pkgname=dbus-glib
pkgname=lib32-$_pkgname
pkgver=0.102
pkgrel=1
pkgdesc="GLib bindings for DBUS (32 bit)"
arch=('x86_64')
license=('GPL')
url="http://www.freedesktop.org/wiki/Software/DBusBindings"
depends=('lib32-glib2')
makedepends=('gcc-multilib')
options=('!emptydirs')
source=("http://dbus.freedesktop.org/releases/${_pkgname}/${_pkgname}-${pkgver}.tar.gz")
md5sums=('f76b8558fd575d0106c3a556eaa49184')

build() {
    export CC='gcc -m32'
    export CXX='g++ -m32'

    cd ${_pkgname}-${pkgver}
    ./configure \
        --prefix=/usr \
        --libdir=/usr/lib32 \
        --localstatedir=/var \
        --sysconfdir=/etc \
        --enable-static=no \
        --enable-bash-completion=no
  make
}

package() {
  cd ${_pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}/usr"/{bin,include,share}
}
