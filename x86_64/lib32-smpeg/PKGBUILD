# Maintainer: Claudio Kozicky <claudiokozicky@gmail.com>
# Contributor: Celti <celticmadman@gmail.com>
# Contributor: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: JD Steffen <jd@steffennet.org>
# Contributor: Eric Belanger <eric@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

_pkgbasename=smpeg
pkgname=lib32-$_pkgbasename
pkgver=0.4.5
pkgrel=1
pkgdesc="SDL MPEG Player Library (32-bit)"
arch=(x86_64)
url="http://icculus.org/smpeg/"
license=(LGPL)
depends=(lib32-sdl lib32-gcc-libs)
makedepends=(gcc-multilib lib32-mesa lib32-glu svn)
optdepends=('lib32-glu: to use glmovie')
source=("$_pkgbasename-$pkgver::svn://svn.icculus.org/smpeg/tags/release_${pkgver//./_}")
md5sums=(SKIP)

build()
{
    export CC="gcc -m32"
    export CXX="g++ -m32"
    export LIBS="-lX11"
    export PKG_CONFIG_PATH=/usr/lib32/pkgconfig

    cd "$srcdir"/$_pkgbasename-$pkgver
	./autogen.sh
    ./configure --prefix=/usr --libdir=/usr/lib32  --disable-static
    make
}

package()
{
    cd "$srcdir"/$_pkgbasename-$pkgver
    make DESTDIR="$pkgdir" install

    rm -r "$pkgdir"/usr/{bin,include,share}
}
