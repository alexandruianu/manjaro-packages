# Maintainer: Jameson Pugh <imntreal@gmail.com>
# Contributor: Swift Geek < swift geek ât gmail døt cøm>

pkgname=lib32-libappindicator
_pkgbasename=libappindicator
pkgver=12.10.0
pkgrel=2
pkgdesc="Library to allow applications to export a menu into the Unity Menu bar (GTK+ 2 library)"
arch=('i686' 'x86_64')
url="https://launchpad.net/libappindicator"
license=('LGPL')
depends=('lib32-libindicator' 'lib32-libdbusmenu-gtk2')
makedepends=('gobject-introspection' 'vala' 'pygtk' 'perl-xml-libxml' 'dbus-glib' 'gcc-multilib')
options=('!libtool')
source=("http://launchpad.net/libappindicator/${pkgver%.*}/$pkgver/+download/$_pkgbasename-$pkgver.tar.gz"
        'python-gtfo.patch')
md5sums=('41910f2005edee9240da1e53fffcdc12'
         'bd9265bf48cdcceaaa3ddb8232465870')

build() {
export CC='gcc -m32'
export CXX='g++ -m32'
export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

cd "$srcdir/$_pkgbasename-$pkgver"
rm -rf bindings/python

# Fix mono bindings location
sed -i 's|/cli/|/mono/|' bindings/mono/{appindicator-sharp-0.1.pc.in,Makefile.in}
sed -i -e '/^MONO_REQUIRED_VERSION/s:=.*:=9999:' configure || die # comment this to enable mono - stolen from gen2
patch -p1 < "${srcdir}/python-gtfo.patch"

./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
--libdir=/usr/lib32 --disable-static --disable-tests --disable-mono-test --disable-introspection --disable-gtk-doc-html --disable-maintainer-mode --disable-mono
make -j1
}

 

package() {
cd "$srcdir/$_pkgbasename-$pkgver"

make DESTDIR="$pkgdir/" install
rm -rf "${pkgdir}"/usr/{include,share,bin}
}

# vim:set ts=2 sw=2 et:
